
module System.IO.Console
export
{       readl;
        write; writel; writell;
        flush;

        takeTextLit;
}

import Data.Text

import foreign abstract type
 Console  : Effect

import foreign c value
 primStdoutPutString    : TextLit# -> S Console Void#
 primStdoutPutVector    : {@r: Region} -> Vector# r Word8# -> S Console Void#
 primStdoutFlush        : Unit -> S Console Void#

 -- ISSUE #425: In foreign impot adding (S Console) to result type causes
 -- codegen problem and runtime crash.
 primStdinGetVector     : {@r: Region} -> Nat# -> Vector# r Word8#


import foreign c value
 -- Get the unboxed text literal from a boxed text literal.
 -- The unboxed version is a pointer to the text data in static memory.
 takeTextLit   : TextLit -> TextLit#

where


-- | Read a text string from the console, up to the maximum line length.
readl (maxLen: Nat): S Console Text
 = box weakeff Console in
   do   vec     = primStdinGetVector maxLen
        TextVec vec

-- | Write text to the console.
write (tt: Text): S Console Unit
 = box case tt of
        TextLit lit
         -> do  primStdoutPutString (takeTextLit lit)
                ()

        TextVec vec
         -> do  primStdoutPutVector vec
                ()

        TextApp t1 t2
         -> do  write t1
                write t2


-- | Write text to the console with a trailing newline.
writel  (tt: Text): S Console Unit
 = do   write tt
        write "\n"


-- | Write text to the console with two trailing newlines.
writell (tt: Text): S Console Unit
 = do   write tt
        write "\n\n"


-- | Flush the output.
flush: S Console Unit
 = box do
        primStdoutFlush ()
        ()
